/*
注意：如要更改版本请修改version.properties
 */

buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}

apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'idea'

Properties versionProperties = new Properties();
versionProperties.load(new StringReader(project.file('version.properties').getText('UTF-8')))

version = versionProperties.getOrDefault('modVersion', 'unknown')
group = 'com.gonggongjohn.eok' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'eok'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

repositories {
    maven {
        name = 'AliyunMaven'
        url = 'http://maven.aliyun.com/nexus/content/groups/public'
    }
    maven {
        // location of the maven that hosts JEI files
        name = 'Progwml6 maven'
        url = 'https://dvs1.progwml6.com/files/maven/'
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        // TOP mirror
        name = 'ModMaven'
        url = 'https://modmaven.k-4u.nl'
    }
    maven {
        // CuckooLib
        name = 'CuckooMaven'
        url = 'https://zi-jing.github.io/cuckoo-maven/maven'
    }
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'
        expand 'version': project.version
    }
}

minecraft {
    // The mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   Snapshot are built nightly.
    // stable_#            Stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: 'snapshot', version: '20200514-1.16'
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')

            if (project.hasProperty('username'))
                args '--username', project.getProperty('username')

            if (project.hasProperty('password'))
                args '--password', project.getProperty('password')

            if (project.hasProperty('memory'))
                jvmArgs '-Xmx' + project.getProperty('memory')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                eok {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                eok {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'examplemod', '--all', '--output', file('src/generated/resources/')

            mods {
                eok {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    // Specify the version of Minecraft to use, If this is any group other then 'net.minecraft' it is assumed
    // that the dep is a ForgeGradle 'patcher' dependency. And it's patches will be applied.
    // The userdev artifact is a special name and will get all sorts of transformations applied to it.
    minecraft 'net.minecraftforge:forge:1.16.1-32.0.75'

    // JEI
    compileOnly fg.deobf('mezz.jei:jei-1.16.1:7.0.0.6:api')
    runtimeOnly fg.deobf('mezz.jei:jei-1.16.1:7.0.0.6')

    // TOP
    compileOnly fg.deobf('mcjty.theoneprobe:TheOneProbe-1.16:1.16-3.0.1-beta-4:api')
    runtimeOnly fg.deobf('mcjty.theoneprobe:TheOneProbe-1.16:1.16-3.0.1-beta-4')

    String cuckooLibVersion = '3.0.0.0'
    if (project.hasProperty('CuckooLibIncludeBuildPath')) {
        compile('com.github.zi_jing.cuckoolib:CuckooLib:' + cuckooLibVersion) {
            transitive = false
        }
    } else {
        compile(fg.deobf('com.github.zi_jing.cuckoolib:CuckooLib:' + cuckooLibVersion)) {
            transitive = false
        }
    }

}

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    manifest {
        attributes([
                'Specification-Title'     : 'eok',
                'Specification-Vendor'    : 'eok',
                'Specification-Version'   : '1', // We are version 1 of ourselves
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : '${version}',
                'Implementation-Vendor'   : 'eok',
                'Implementation-Timestamp': new Date().format('yyyy-MM-dd\'T\'HH:mm:ssZ')
        ])
    }
}

// Example configuration to allow publishing using the maven-publish task
// This is the preferred method to reobfuscate your jar file
jar.finalizedBy('reobfJar')
// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing
//publish.dependsOn('reobfJar')

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier 'sources'
    afterEvaluate {
        finalizedBy reobfJar
    }
}

task apiJar(type: Jar) {
    from sourceSets.main.allSource
    include 'com/gonggongjohn/eok/api/**'
    classifier 'api'
    afterEvaluate {
        finalizedBy reobfJar
    }
}

artifacts {
    archives jar
    archives sourceJar
    archives apiJar
}

publishing.publications {
    mavenJava(MavenPublication) {
        artifact jar
        artifact sourceJar
        artifact apiJar
    }
}

publishing.repositories {
    maven {
        url 'file:///${project.projectDir}/mcmodsrepo'
    }
}


tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

if (JavaVersion.current().isJava8Compatible()) {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}