import java.nio.charset.StandardCharsets
import java.nio.file.Files

/*
注意：如要更改版本请修改version.properties
 */

buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath group: 'commons-io', name: 'commons-io', version: '2.7'
    }
}

apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'idea'

def cuckooLibPath
if (project.hasProperty('CuckooLibProjectPath')) {
    def cpath = (project.property('CuckooLibProjectPath') as String).replace('\\', '/')
    cuckooLibPath = cpath.endsWith('/') ? cpath : cpath + '/'
} else {
    cuckooLibPath = ''
}

def versionProperties = new Properties();
versionProperties.load(new StringReader(project.file('version.properties').getText('UTF-8')))

version = versionProperties.getOrDefault('modVersion', 'unknown')
group = 'com.gonggongjohn.eok' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'eok'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

repositories {
    if (project.hasProperty('useLocalBuildMaven') && project.property('useLocalBuildMaven') == 'true') {
        maven {
            name = 'LocalBuildMaven'
            url = 'file:///' + project.projectDir.absolutePath + '/LocalBuildMaven'
        }
    }
    if (project.hasProperty('CuckooLibProjectPath')) {
        def file = new File(cuckooLibPath)
        if (file.exists()) {
            maven {
                name = 'CuckooLibLocalBuildMaven'
                url = 'file:///' + file.absolutePath + '/LocalBuildMaven'
            }
        }
    }
    maven {
        name = 'AliyunMaven'
        url = 'http://maven.aliyun.com/nexus/content/groups/public'
    }

    maven {
        // JEI
        name = 'Progwml6 maven'
        url = 'https://dvs1.progwml6.com/files/maven/'
    }

    maven {
        // TOP
        name 'tterrag maven'
        url "https://maven.tterrag.com/"
    }
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'
        expand 'version': project.version
    }
}

println '\n########################################################\n'
println ' IMPORTANT: IF YOU WANT TO GENERATE RUN CONTIGURATIONS '
println ' FOR INTELLIJ IDEA, YOU MUST RUN THE "genIntellijRunsForEOK"'
println ' TASK INSTEAD OF "genIntellijRuns" !!!!!!!!'
println '\n########################################################\n'

// 用暴力方法纠正EOK启动方案中的错误
def fixRunConfiguration(fileName) {
    def confRoot = rootProject.projectDir.getAbsolutePath() + '/.idea/runConfigurations'
    def lines = Files.readAllLines(new File(confRoot + '/' + fileName).toPath(), StandardCharsets.UTF_8)
    def clFile = new File(project.property('CuckooLibProjectPath') as String).getAbsoluteFile()
    def clPath = clFile.getAbsolutePath().replace('\\', '/')
    clPath += clPath.endsWith('/') ? '' : '/'
    def builder = new StringBuilder()
    for (int i = 0; i < 6; i++)
        builder.append(' ')
    def lineNumber = 1
    for (def str : lines) {
        if (str.contains('name="MOD_CLASSES"')) {
            break
        } else {
            lineNumber++
        }
    }
    if (lineNumber == 1) {
        System.err.println('ERROR: Run Configuration corruption!')
        throw new IllegalStateException('Run Configuration corruption!')
    }
    builder.append('<env name="MOD_CLASSES" value="')
    builder.append('cuckooLib%%')
    builder.append(clPath)
    builder.append('build/resources/main;')
    builder.append('cuckooLib%%')
    builder.append(clPath)
    builder.append('build/classes/java/main;')
    builder.append('eok%%$PROJECT_DIR$/build/resources/main;')
    builder.append('eok%%$PROJECT_DIR$/build/classes/java/main"')
    builder.append(' />')
    lines.set(lineNumber - 1, builder.toString())
    def outputBuilder = new StringBuilder()
    for (def str : lines) {
        outputBuilder.append(str)
        outputBuilder.append('\n')
    }
    new File(confRoot + '/' + fileName).setText(outputBuilder.toString(), 'UTF-8')
}

// 用暴力方法纠正EOK启动方案中的错误
def genIntellijRunsForEOK = tasks.create('genIntellijRunsForEOK') {
    group = 'fg_runs'
    description = 'Generates the run configurations for EOK, please use this instead of "genIntellijRuns"'
    doLast {
        if (project.hasProperty('CuckooLibProjectPath')) {
            fixRunConfiguration('runClient.xml');
            fixRunConfiguration('runServer.xml');
            fixRunConfiguration('runData.xml');
        }
    }
}

// 用暴力方法纠正EOK启动方案中的错误
genIntellijRunsForEOK.dependsOn('genIntellijRuns')

minecraft {
    // The mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   Snapshot are built nightly.
    // stable_#            Stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: 'snapshot', version: '20200820-1.16.1'
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')

            if (project.hasProperty('username'))
                args '--username', project.getProperty('username')

            if (project.hasProperty('password'))
                args '--password', project.getProperty('password')

            if (project.hasProperty('memory'))
                jvmArgs '-Xmx' + project.getProperty('memory')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                eok {
                    source sourceSets.main
                }
                if (project.hasProperty('CuckooLibProjectPath')) {
                    cuckooLib {
                        source project(':CuckooLib').sourceSets.main
                    }
                }
            }
        }

        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                eok {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'examplemod', '--all', '--output', file('src/generated/resources/')

            mods {
                eok {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    // Specify the version of Minecraft to use, If this is any group other then 'net.minecraft' it is assumed
    // that the dep is a ForgeGradle 'patcher' dependency. And it's patches will be applied.
    // The userdev artifact is a special name and will get all sorts of transformations applied to it.
    minecraft 'net.minecraftforge:forge:1.16.2-33.0.22'

    // JEI
    compileOnly fg.deobf('mezz.jei:jei-1.16.2:7.1.3.20:api')
    runtimeOnly fg.deobf('mezz.jei:jei-1.16.2:7.1.3.20')

    // TOP
    compileOnly fg.deobf('mcjty.theoneprobe:TheOneProbe-1.16:1.16-3.0.4-beta-7:api')
    runtimeOnly fg.deobf('mcjty.theoneprobe:TheOneProbe-1.16:1.16-3.0.4-beta-7')

    def cuckooLibVersion = versionProperties.getOrDefault('cuckooLibVersion', 'unknown')
    if (project.hasProperty('CuckooLibProjectPath')) {
        compile project(':CuckooLib')
    } else {
        compile(fg.deobf('com.github.zi_jing.cuckoolib:CuckooLib:' + cuckooLibVersion))
    }
}

jar {
    manifest {
        attributes([
                'Specification-Title'     : 'eok',
                'Specification-Vendor'    : 'eok',
                'Specification-Version'   : '1', // We are version 1 of ourselves
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : '${version}',
                'Implementation-Vendor'   : 'eok',
                'Implementation-Timestamp': new Date().format('yyyy-MM-dd\'T\'HH:mm:ssZ')
        ])
    }
}

// Example configuration to allow publishing using the maven-publish task
// This is the preferred method to reobfuscate your jar file
jar.finalizedBy('reobfJar')
// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing
//publish.dependsOn('reobfJar')

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier 'sources'
    afterEvaluate {
        finalizedBy reobfJar
    }
}

task apiJar(type: Jar) {
    from sourceSets.main.allSource
    include 'com/gonggongjohn/eok/api/**'
    classifier 'api'
    afterEvaluate {
        finalizedBy reobfJar
    }
}

artifacts {
    archives jar
    archives sourceJar
    archives apiJar
}

publishing.publications {
    mavenJava(MavenPublication) {
        artifact jar
        artifact sourceJar
        artifact apiJar
    }
}

publishing.repositories {
    publishing.repositories {
        if (project.hasProperty('useLocalBuildMaven') && project.property('useLocalBuildMaven') == 'true') {
            maven {
                name = 'LocalBuildMaven'
                url = 'file:///' + project.projectDir.absolutePath + '/LocalBuildMaven'
            }
        }
    }
}


tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

if (JavaVersion.current().isJava8Compatible()) {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}